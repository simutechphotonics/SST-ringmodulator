#SimuTech Group
#Author: Stephen Lin
#Date: 2023
#Ths script is provided by SimuTech's SimuSkills platform.
#Access to this script is provided along with the TECH support package by SimuTech.
#The content provided should NOT be shared publicly.
#The scripts and files included are for educational purpose for SimuTech Group's customers.
#Customers are free to utilize the scripts and files in their own product design flows.

#This is a setup script that automatically generates geometry based on input parameters
#Simulation time ETA 4 hours.

#INITIALIZE
newproject;#Clear project contents
clear;#clear script workspace variables

#PARAMETERS
#Units: ums
thick_Si = 0.22; #height of wg
width_Si = 0.5; #width of wg
thick_Clad = 2.0;
thick_BOX = 2.0; 
sidewall_angle = 90;

ring_radius = 10; #1.55/2/3.14/3.4784
gap = 0.200;
wavelength_start = 1.5; #um
wavelength_stop = 1.6;
racetrack_length = 5;

#additional options
thick_Slab = 0; #include if slab, use 0 for strip waveguides
bool_half_ring = false;
bool_racetrack = false;

#CONVERSION of UNITS
#ums
thick_Clad = thick_Clad*1e-6;
thick_Si = thick_Si*1e-6;
thick_BOX = thick_BOX*1e-6;
thick_Slab = thick_Slab*1e-6;
width_Si = width_Si*1e-6;
ring_radius = ring_radius*1e-6;
gap = gap*1e-6;
wavelength_start = wavelength_start*1e-6;
wavelength_stop = wavelength_stop*1e-6;
racetrack_length = racetrack_length*1e-6;

#MATERIALS
material_Clad = "SiO2 (Glass) - Palik";
material_BOX = "SiO2 (Glass) - Palik";
material_Si = "Si (Silicon) - Palik";

#DEFINE SIMULATION REGION
sim_width = 2.0e-6; #span amount around wg
sim_height = 1.0e-6; #span amount around wg
Zmin = -sim_height;
Zmax = thick_Si+sim_height;

#CREATE GEOMETRY
#draw cladding
addrect; #Creates a rectangle geometry object. Newly created objects are automatically selected
set("name","cladding"); #sets parameters of the SELECTED object. use select() with a name in string format as the argument to select
set("material",material_Clad);
set("x",0);
set("x span", ring_radius*6);
set("y",0);
set("y span",ring_radius*4);
set("z min",0);
set("z max", thick_Clad);
set("override mesh order from material database", 1); #enables custom mesh orders
set("mesh order",3); #sets the importance of the object when simulation runs, this puts cladding at the very back (least important)
set("alpha",0.3); #optional: adjust the visual transparency

#draw BOX
addrect;
set("name","BOX");
set("material",material_BOX);
set("x",0);
set("x span", ring_radius*6);
set("y",0);
set("y span",ring_radius*4);
set("z min",-thick_BOX);
set("z max",0);
set("override mesh order from material database", 1);
set("mesh order",3);
set("alpha",0.4);

#draw wg
addwaveguide;
set("name","waveguide");
set("material",material_Si);
set("z",thick_Si/2);
set("poles",[-ring_radius*2,ring_radius+gap+width_Si;ring_radius*2,ring_radius+gap+width_Si]);
set("base angle",sidewall_angle); #ring angle
set("base width",width_Si); #ring wg width
set("base height",thick_Si); #ring wg height
set("override mesh order from material database", 1);
set("mesh order",1);

#draw wg
#copy;
#set("name","waveguide2");
#set("poles",[-ring_radius*2,-ring_radius-gap-width_Si;ring_radius*2,-ring_radius-gap-width_Si]);

#draw ring
addwaveguide;
set("name","ring_q1");
set("material",material_Si);
#bezier curve approximation of a circle, please use more poles if smoother circle is necessary
#https://www.researchgate.net/publication/265893293_Approximation_of_a_cubic_bezier_curve_by_circular_arcs_and_vice_versa
#https://stackoverflow.com/questions/734076/how-to-best-approximate-a-geometrical-arc-with-a-bezier-curve
set("poles",[0,ring_radius;ring_radius*0.5522847498,ring_radius;ring_radius,ring_radius*0.5522847498;ring_radius,0]); #bezier curve circle approximation, 1e-6 is necessary for units
set("base angle",sidewall_angle); #ring angle
set("base width",width_Si); #ring wg width
set("base height",thick_Si); #ring wg height
set("z",thick_Si/2);
set("override mesh order from material database", 1);
set("mesh order",1);
set("x",racetrack_length/2);
addtogroup('ring');
copy;
set("name","ring_q4");
set("first axis","z");
set("rotation 1",-270);
set("x",-racetrack_length/2);

#Racetrack
if(racetrack_length!=0){
    addwaveguide;
    set("name","racetrack");
    set("material",material_Si);
    set("z",thick_Si/2);
    set("poles",[-racetrack_length/2,ring_radius;racetrack_length/2,ring_radius]);
    set("base angle",sidewall_angle); #ring angle
    set("base width",width_Si); #ring wg width
    set("base height",thick_Si); #ring wg height
    set("override mesh order from material database", 1);
    set("mesh order",1);
    addtogroup('ring');
}

#draw slab
if(thick_Slab!=0){
    addrect;
    set("name","slab");
    set("material",material_Si);
    set("x",0);
    set("x span", ring_radius*6);
    set("y",0);
    set("y span",ring_radius*4);
    set("z min",0);
    set("z max",thick_Slab);
    set("override mesh order from material database", 1);
    set("mesh order",1);
    set("alpha",0.5);
}

#CREATE SOLVER OBJECT
addfdtd;
set("x",0);
set("y",0);
set("z",0);
set("x span",ring_radius*3+racetrack_length/2);
set("y max",ring_radius*1.5);
set("y min",-0.1e-6); #extend by 0.1 um for ports 
set("z span",thick_Si*5);
set("dimension","3D");
set("y max bc","metal");
##Port 1
addport;
set('x',-1.3*ring_radius);
set('y',ring_radius+gap+width_Si);
set('y span', width_Si*3);
set("z",thick_Si/2);
set("z span",thick_Si*4);
##Port 2
addport;
set('x',1.3*ring_radius);
set('y',ring_radius+gap+width_Si);
set('y span', width_Si*3);
set("z",thick_Si/2);
set("z span",thick_Si*4);
set("direction","Backward");
##Port 3
addport;
set('injection axis','y');
set('x',-ring_radius-racetrack_length/2);
set('x span', width_Si*3);
set("y",0);
set("z",thick_Si/2);
set("z span",thick_Si*4);
set("direction","Forward");
##Port 4
addport;
set('injection axis','y');
set('x',ring_radius+racetrack_length/2);
set('x span', width_Si*3);
set("y",0);
set("z",thick_Si/2);
set("z span",thick_Si*4);
set("direction","Forward");
select("FDTD::ports");
set("monitor frequency points",100); #how fine the frequency sweep is

#wg to extend past fdtd region
#This object is used to make sure the FDTD boundary is properly simulating the WG.
#The objects outside the FDTD region will not be simulated at all.
addwaveguide;
set("name","boundary_wg1");
set("material",material_Si);
set("z",thick_Si/2);
set("poles",[-ring_radius-racetrack_length/2,0;-ring_radius-racetrack_length/2,-ring_radius]);
set("base angle",sidewall_angle); #ring angle
set("base width",width_Si); #ring wg width
set("base height",thick_Si); #ring wg height
set("override mesh order from material database", 1);
set("mesh order",1);
copy;
set("name","boundary_wg2");
set("poles",[ring_radius+racetrack_length/2,0;ring_radius+racetrack_length/2,-ring_radius]);

#CREATE MESH
addmesh;
set('name','CouplingMesh');
set('x',0);
set('y',ring_radius+gap+width_Si/2);
set('z',0);
set('y span',width_Si*5);
set('x span',racetrack_length+3e-6); #Manually defined size for coupling area
set("override x mesh",false);
set("dy",0.01e-6); #mesh fine steps amount
set("dz",0.01e-6); #mesh fine steps amount

#SET SIMULATION PARAMETERS
##Wavelength
setglobalsource("wavelength start",wavelength_start);
setglobalsource("wavelength stop",wavelength_stop);

#Add s-param sweep
addsweep(3);
setsweep("s-parameter sweep", "name", "SParam");
setsweep("SParam", "Excite all ports", false);
setsweep("SParam", "auto symmetry", true);

#Very nonstandard struct by lumerical here, hopefully future versions improve this
#the struct contains fields that have spaces in them, so you can't use the dot notation to call them
#the struct can be dissected and observed using getfield(struct)
#each layer needs to be assigned to a variable and slowly deconstructed
#e.g. while selecting the sweep:
#a = getsweep("SParam,"export setup");
#b = getfield("export field","port 1");
#b2 = getfield(a,"mode 1");
#?b2.label
#OUPUT: "mode 1"
  
param_export = {
    "port 1":{ #port 1 is a STRUCT with the fields "mode 1" and "location"
        "mode 1":{ #mode 1 is a STRUCT contains two layers, the id and the label
            "id":1, #the id is a INT value
            "label":"mode 1" #the label is a STR value
            },
        "location":"left"}, #the location is a STR value, at the moment only allows "left","right"
    "port 2":{
        "mode 1":{
            "id":1,
            "label":"mode 1"
            },
        "location":"right"},
    "port 3":{
        "mode 1":{
            "id":1,
            "label":"mode 1"
            },
        "location":"BOTTOM"}, #cant set this in script yet..
    "port 4":{
        "mode 1":{
            "id":1,
            "label":"mode 1"
            },
        "location":"BOTTOM"} #cant set this in script yet..
};
    
setsweep("SParam", "export setup", param_export);
